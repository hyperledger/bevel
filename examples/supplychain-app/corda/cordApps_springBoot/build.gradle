buildscript {
    ext {
        corda_release_group = 'net.corda'
        corda_release_version = '4.4'             
        corda_gradle_plugins_version = '5.0.6'  
        kotlin_version = '1.2.71'
        junit_version = '4.12'
        testng_version = '6.10'
        quasar_version = '0.7.12_r3'
        spring_boot_version = '2.1.3.RELEASE'
        spring_boot_gradle_plugin_version = '2.0.2.RELEASE'
        springfox_version = '2.9.2'
        slf4j_version = '1.7.25'
        log4j_version = '2.11.2'     
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_gradle_plugin_version"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    }
}

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'org.sonarqube'
apply plugin: 'maven-publish'

publishing{
    publications {
    maven1(MavenPublication) {
        artifactId = 'cordapp-supply-chain'
        artifact('build/cordapp-supply-chain-0.1.jar')
            }
    maven2(MavenPublication) {
        artifactId = 'cordapp-contracts-states'
        artifact('build/cordapp-contracts-states-0.1.jar')
            }
        }
        repositories {
        maven {
            url project.repoURI
            credentials {
                username project.repoUser
                password project.repoPassword
                }
            }
        }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG() {
        useDefaultListeners = true
    }
    reports.html.enabled = true
}

dependencies {

    // Corda dependencies.
    testCompile "net.corda:corda-node-driver:$corda_release_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.testng:testng:$testng_version"
    testCompile 'com.ea.agentloader:ea-agent-loader:1.0.3'


    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    cordaCompile "$corda_release_group:corda-node-api:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"

    // CorDapp dependencies
    // Specify your CorDapp's dependencies below, including dependent CorDapps.
    cordapp project(":cordapp-supply-chain")
    cordapp project(":cordapp-contracts-states")

    cordaCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    cordaCompile "org.apache.logging.log4j:log4j-web:${log4j_version}"
    cordaCompile "org.slf4j:jul-to-slf4j:$slf4j_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}


task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    directory "./build/nodes"
    node {
        name "O=Notary,L=51.50/-0.13/London,C=GB"
        notary = [validating : false]
        p2pAddress "notary:10010"
        rpcSettings {
            address("notary:10011")
            adminAddress("notary:10012")
           
        }
        sshdPort 2222
        cordapps = [
                "$project.group:cordapp-contracts-states:$project.version",
                "$project.group:cordapp-supply-chain:$project.version"
        ]
    }
    node {
        name "O=PartyA,OU=Manufacturer,L=47.38/8.54/Zurich,C=CH"
        p2pAddress "partya:10010"
        rpcSettings {
            address("partya:10011")
            adminAddress("partya:10012")
        }
        sshdPort 2222
        cordapps = [
                "$project.group:cordapp-contracts-states:$project.version",
                "$project.group:cordapp-supply-chain:$project.version"
        ]
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=PartyB,OU=Carrier,L=51.50/-0.13/London,C=US"
        p2pAddress "partyb:10010"
        rpcSettings {
            address("partyb:10011")
            adminAddress("partyb:10012")
        }
        sshdPort 2222
        cordapps = [
                "$project.group:cordapp-contracts-states:$project.version",
                "$project.group:cordapp-supply-chain:$project.version"
        ]
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=PartyC,OU=Warehouse,L=42.36/-71.06/Boston,C=US"
        p2pAddress "partyc:10010"
        rpcSettings {
            address("partyc:10011")
            adminAddress("partyc:10012")
        }
        sshdPort 2222
        cordapps = [
                "$project.group:cordapp-contracts-states:$project.version",
                "$project.group:cordapp-supply-chain:$project.version"
        ]
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=PartyD,OU=Store,L=40.73/-74/New York,C=US"
        p2pAddress "partyd:10010"
        rpcSettings {
            address("partyd:10011")
            adminAddress("partyd:10012")
        }
        sshdPort 2222
        cordapps = [
                "$project.group:cordapp-contracts-states:$project.version",
                "$project.group:cordapp-supply-chain:$project.version"
        ]
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}

task deployWebapps(type: Copy, dependsOn: [
    'webserver-supply-chain:deployWebapp',
]) {
    ext.webappDir = file("build/webapps")

    from('webserver-supply-chain/build/webapp') { include '**/*.jar' }

    // this should be for each webmodule. such as :
    from("webserver-supply-chain/src/test/resources/scripts/") {
        filter {
            it.replace('#DIR#', ext.webappDir.getAbsolutePath())
        }
    }
    into ext.webappDir
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
    }
}
